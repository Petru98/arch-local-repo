_pkgname=CLI11
pkgname=cli11
pkgver=1.9.1
pkgrel=1
pkgdesc='Command line parser for C++11'
arch=('x86_64')
url='https://github.com/CLIUtils/CLI11'
license=('BSD')

makedepends=('cmake')

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/CLIUtils/CLI11/archive/v${pkgver}.tar.gz")
sha512sums=('bcccfe88695ceddd73d87738e1d5ed4372f5cf43e3866e88053dd486ea8973804022029775de781e1dbdd2e1566b77e072de00202033983217276954a07c5718')


build() {
    cd "${_pkgname}-${pkgver}"
    CXX=${CXX:-g++} cmake -H. -Bbuild \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCLI11_BUILD_TESTS=OFF \
        -DCLI11_BUILD_EXAMPLES=OFF

    cmake --build build
}

package() {
    cd "${_pkgname}-${pkgver}"
    cmake --build build -- DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
