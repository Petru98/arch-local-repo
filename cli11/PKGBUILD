_pkgname=CLI11
pkgname=cli11
pkgver=2.0.0
pkgrel=1
pkgdesc='Command line parser for C++11'
arch=('x86_64')
url='https://github.com/CLIUtils/CLI11'
license=('BSD')

makedepends=('cmake')

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/CLIUtils/CLI11/archive/v${pkgver}.tar.gz")
sha512sums=('5ac06623383c003286b915e585e85ad419449735770e9a164665395f33d9e03ec6340445ab782200e2cfe5f40123b5843a7401211b6ece5b16342321e3e7a10a')


build() {
    cd "${_pkgname}-${pkgver}"
    CXX=${CXX:-g++} cmake -H. -Bbuild \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCLI11_BUILD_TESTS=OFF \
        -DCLI11_BUILD_EXAMPLES=OFF

    cmake --build build
}

package() {
    cd "${_pkgname}-${pkgver}"
    cmake --build build -- DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
