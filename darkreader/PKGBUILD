pkgbase=darkreader
pkgname=(librewolf-$pkgbase chromium-$pkgbase)
pkgver=4.9.34
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=(any)
url='https://darkreader.org'
license=(MIT)
makedepends=(npm unzip)
source=("https://github.com/darkreader/darkreader/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=(692ac9ab7992a7b78bf35499946b1e0cf5e31c9d396dade52a1f967489a5b15dd92ad7d99154e90e7114a10113c4d06e459010d69b064bb477a04e15098918cf)

build() {
    cd $pkgbase-$pkgver
    npm install
    npm run build
}

check() {
    cd $pkgbase-$pkgver
    npm test
}

package_librewolf-darkreader() {
    groups=(librewolf-addons)
    cd $pkgbase-$pkgver
    install -Dm644 "build-firefox.xpi" "$pkgdir/usr/lib/librewolf/browser/extensions/addon@darkreader.org.xpi"
}

package_chromium-darkreader() {
    groups=(chromium-addons)
    cd $pkgbase-$pkgver
    install -dm755 "$pkgdir/usr/share/chromium-extension-$pkgbase"
    unzip "build.zip" -d "$pkgdir/usr/share/chromium-extension-$pkgbase"
}
