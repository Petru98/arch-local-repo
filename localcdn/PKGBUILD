pkgbase=localcdn
pkgname=(librewolf-$pkgbase chromium-$pkgbase)
pkgver=2.6.10
pkgrel=1
pkgdesc='A web browser extension that emulates Content Delivery Networks to improve your online privacy'
arch=(any)
url='https://www.localcdn.org'
license=(MPL2)
makedepends=(zip)
source=("$pkgbase-$pkgver.tar.gz::https://codeberg.org/nobody/LocalCDN/archive/v$pkgver.tar.gz")
sha512sums=(112705c984ec71e0450007f13c191f4cbc592ad5ad419195809324bd9248176f9b52f7855393dd7bf618681fe9192094f91092f365fb3359eda31f70d41c72d5)

build() {
    cd "$pkgbase"
    find . -maxdepth 1 \( -name '*.git*' -o -name 'screenshots' -o -name '*.txt' -o -name '*.md' -o -name '.*' \) -exec rm -rf '{}' \;
    zip -r --wild-stop-dirs "../$pkgbase.xpi" ./*
}

package_librewolf-localcdn() {
    groups=(librewolf-addons)
    install -Dm644 "$pkgbase.xpi" "$pkgdir/usr/lib/librewolf/browser/extensions/{b86e4813-687a-43e6-ab65-0bde4ab75758}.xpi"
}

package_chromium-localcdn() {
    groups=(chromium-addons)
    install -dm755 "$pkgdir/usr/share/"
    mv -T "$pkgbase" "$pkgdir/usr/share/chromium-extension-$pkgbase"
}
