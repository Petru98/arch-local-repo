_name=SponsorBlock
pkgbase=sponsorblock
pkgname=(librewolf-$pkgbase chromium-$pkgbase)
pkgver=2.1.4
pkgrel=1
pkgdesc='Skip YouTube video sponsors'
arch=(any)
url='https://sponsor.ajay.app'
license=(GPL3)
makedepends=(npm zip)
source=("https://github.com/ajayyy/$_name/archive/refs/tags/$pkgver.tar.gz")
sha512sums=(7670e8a78f6a4a0a4896e38a115bdd2fdf2aa2f5d8c6fe113ba6a3a3ec7b4ae11c280492e3ce53af4150342207bddf85e4249f3d2bcbca188b9057ffff951440)

prepare() {
    cd $_name-$pkgver
    mv config.json.example config.json
}

build() {
    cd $_name-$pkgver
    npm install
    npm run build:firefox
    pushd dist
    zip -r --wild-stop-dirs "../$pkgbase.xpi" ./*
    popd
    rm -r ./dist/*
    npm run build
}

package_librewolf-sponsorblock() {
    groups=(librewolf-addons)
    cd $_name-$pkgver
    install -Dm644 "$pkgbase.xpi" "$pkgdir/usr/lib/librewolf/browser/extensions/sponsorBlocker@ajay.app.xpi"
}

package_chromium-sponsorblock() {
    groups=(chromium-addons)
    cd $_name-$pkgver
    install -dm755 "$pkgdir/usr/share/"
    mv -T ./dist "$pkgdir/usr/share/chromium-extension-$pkgbase"
}
